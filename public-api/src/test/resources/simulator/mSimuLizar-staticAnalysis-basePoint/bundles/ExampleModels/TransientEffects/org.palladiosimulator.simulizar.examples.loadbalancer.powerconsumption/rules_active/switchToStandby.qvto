/*
 * This simple QVTO SimuLizar reconfiguration rule increases the processing rate of all
 * resources in case the monitored mean response time is greater than 3.0 (seconds). 
 *
 */
import org.palladiosimulator.simulizar.action.repository.black.SimulationStateLibrary;
/*
 * Two modeltypes are declared. The http NS URIs correspond to those used to register the 
 * Ecore models in the environment. Alternatively, a workspace metamodel may be used 
 * in conjunction with mappings defined in the project properties.
 */
modeltype RUNTIMEMEASUREMENT uses 'http://simulizar.palladiosimulator.org/RuntimeMeasurement/1.0';
modeltype PCM_ALLOC uses 'http://palladiosimulator.org/PalladioComponentModel/Allocation/5.2';
modeltype POWER_INF uses 'http://www.fzi.de/Power/Infrastructure/1.0';
modeltype ACTIONS uses 'http://simulizar.palladiosimulator.org/Actions/Core/1.1';
modeltype INSTANCE uses 'http://simulizar.palladiosimulator.org/Actions/Instance/1.1';
modeltype PCM_MP uses 'http://palladiosimulator.org/PCM/MeasuringPoint/1.0';
modeltype ECORE uses 'http://www.eclipse.org/emf/2002/Ecore';

/*
 * The transformation signature declares that a PRM modeltype is required as input, while an PCM
 * modeltype is transformed inplace. The PRM modeltype is referenced as 'prm' throughout the 
 * transformation definition, while no name is needed for the output PCM modeltype. Note that OCL 
 * type and namespace notation are used in operational QVT (: and :: respectively).
 */
transformation SwitchToStandby(		
							in prm : RUNTIMEMEASUREMENT,
							inout pcmAllocation : PCM_ALLOC,
							inout powerInfrastructure : POWER_INF,
							inout actionsInput : ACTIONS,
							out roleSets : INSTANCE) {
							
							
	property adaptationBehaviorRepository = actionsInput.rootObjects()[core::AdaptationBehaviorRepository]->any(true);
	property changePowerStateAdaptationBehaviorId : String = '_Ziv7fLerarHsaygm-zjfH';
	property affectedResourceRoleId : String = '_AfvooYn0EeWm8ose-zjfHg';
	property desiredPowerStateRoleId : String = '_P-SaQYn0EeWm8ose-zjfHg';
	property currentAllocationRoleId : String = '_7ldKUZTzEeWUYbZGIbhTfA';

	/**
	 * The main transformation.
	 * This is the entry point to the overall transformation.
	 */
	main() {
	
		//log('SimuLizar \'Switch To Standby\' Rule Transformation started');
		
		var runtimeMeasurements = prm.rootObjects()[RuntimeMeasurement];
		var currentAllocation = pcmAllocation.rootObjects()[Allocation]->any(true);
		
		assert fatal(runtimeMeasurements->notEmpty())
			with log ('No Measurements found!');
			
		assert fatal (not currentAllocation.oclIsUndefined()) 
			with log ('No valid allocation model found!');
			
		powerInfrastructure.objectsOfType(StatefulPowerConsumingResourceSet)[powerState.name = "ON" and id = "_pElrkoI5EeWCRu5beQNLXQ"]->forEach(runningStatefulResource) {
			if (runningStatefulResource <> null and getSimulationTime() > 0.0) {
				var idleMeasurementsOfResource := runtimeMeasurements->filterIdleUtilMeasurementsOfResource(runningStatefulResource);
				if (idleMeasurementsOfResource->notEmpty()) {
					var changePowerStateAdaptationBehavior : AdaptationBehavior := adaptationBehaviorRepository.actions[id = changePowerStateAdaptationBehaviorId]->any(true);
					var adaptationBehaviorRoleSet := object RoleSet@roleSets {
						//the desired power state
						roles += object instance::Role {
							roleType := changePowerStateAdaptationBehavior.involvedRoles[id = desiredPowerStateRoleId]->any(true);
							value := runningStatefulResource.statefulResourcePowerBinding.powerStateBindings->any(name = "SHUTDOWN").oclAsType(EObject);
						};
						//the affected stateful resource
						roles += object instance::Role {
							roleType := changePowerStateAdaptationBehavior.involvedRoles[id=affectedResourceRoleId]->any(true);
							value := runningStatefulResource.oclAsType(EObject);
						};
						//finally, pass the current allocation
						roles += object instance::Role {
							roleType := changePowerStateAdaptationBehavior.involvedRoles[id=currentAllocationRoleId]->any(true);
							value := currentAllocation.oclAsType(EObject);
						};
					};
					changePowerStateAdaptationBehavior.execute(adaptationBehaviorRoleSet);
					log("Change of Power State executed");
					log("Changed power state of stateful resource to: " + runningStatefulResource.powerState.name);
				};
			};
		};
	}
	
	query Set(RuntimeMeasurement) :: filterIdleUtilMeasurementsOfResource(in resource : StatefulPowerConsumingResourceSet) : Set(RuntimeMeasurement) {
		return self->filterEmptyUtilizationMeasuements()[measuringPoint.oclIsTypeOf(ActiveResourceMeasuringPoint) and measuringPoint.oclAsType(ActiveResourceMeasuringPoint).activeResource.id = resource.processingResourceSpecifications->any(true).id];
	}
	
	query Set(RuntimeMeasurement) :: filterEmptyUtilizationMeasuements() : Set(RuntimeMeasurement) {
		return self[measurementSpecification.metricDescription.name = "Utilization of Active Resource Tuple" and measuringValue = 0.0];
	}
}